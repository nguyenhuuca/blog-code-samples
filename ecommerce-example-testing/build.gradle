plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

group = 'com.ttulka.blog.samples'
version = '1.0.0-SNAPSHOT'

dependencies {
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	runtimeOnly 'com.h2database:h2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'org.testcontainers:testcontainers:1.15.0'
	testImplementation 'org.testcontainers:junit-jupiter:1.15.0'
	testImplementation 'org.seleniumhq.selenium:htmlunit-driver:2.45.0'
}

bootJar {
	archiveName 'application.jar'	// referenced in Testcontainers
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
}

test {
	useJUnitPlatform()

	filter {
		includeTestsMatching '*Test'
	}
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	useJUnitPlatform()

	filter {
		includeTestsMatching '*TestIT'
	}

	shouldRunAfter test
}

task end2endTest(type: Test) {
	description = 'Runs end-to-end tests.'
	group = 'verification'

	useJUnitPlatform()

	filter {
		includeTestsMatching '*TestE2E'
	}

	dependsOn bootJar

	shouldRunAfter integrationTest
}

task uiTest(type: Test) {
	description = 'Runs UI tests.'
	group = 'verification'

	useJUnitPlatform()

	filter {
		includeTestsMatching '*TestUI'
	}

	dependsOn bootJar

	shouldRunAfter end2endTest
}

check.dependsOn(integrationTest, end2endTest, uiTest)

repositories {
	mavenCentral()
}
